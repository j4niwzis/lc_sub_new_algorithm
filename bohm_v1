def A = \x.\y.(y (x x y));;
def Y = (A A);;

{Church Numerals}
def zero  = \f. \x. x;;
def one   = \f. \x. (f x);;
def two   = \f. \x. (f (f x));;
def three = \f. \x. (f (f (f x)));;
def four  = \f. \x. (f (f (f (f x))));;
def five  = \f. \x. (f (f (f (f (f x)))));;
def six   = \f .\x. (f (f (f (f (f (f x))))));;
def seven = \f .\x. (f (f (f (f (f (f (f x)))))));;
def eight = \f .\x. (f (f (f (f (f (f (f (f x))))))));;
def nine  = \f .\x. (f (f (f (f (f (f (f (f (f x)))))))));;
def ten   = \f .\x. (f (f (f (f (f (f (f (f (f (f x))))))))));;

def T = \t.\f. (t);;
def F = \t.\f. (f);;

def pair = \a.\b.\c. (c a b);;

def second = \p. (p F);;

def stream = (Y \stream.(\f.\x.\c. (c (f x) (stream f (f x)))));;

def sub_init = \b.\f.\x.
  (pair x (b (pair x) (stream f x)));;

def sub = \a.\b.\f.\x.
  ((a second (sub_init b f x)) T);;

def pred_init = \f.\x. (pair x (pair x (stream f x)));;

def pred = \a.\f.\x. ((a second (pred_init f x)) T);;

def eq_stream = (Y \eq_stream.
  \c. (c F eq_stream )
);;

def eq_init = \b. (b (pair F) (pair T eq_stream));;

def eq = \a.\b. ((a second (eq_init b)) T);;

def bigger = \a.\b. (sub a b (\x. T) F);;

def lesser = \a.\b. (sub b a (\x. T) F);;

def div_stream = (Y \div_stream.
  (\b.\f.\x. (b (pair x) (div_stream b f (f x) )))
);;

def div_init = div_stream;;

def div2 = \a.\b.\f.\x. ((a second (div_init b f x)) T);;

def to_int = \n. (n (\x.x + 1) 0 );;

(to_int (sub ten five));; {5}
(to_int (pred ten));; {9}
(eq ten ten 1 0);; {1}
(eq ten five 1 0);; {0}
(bigger six five 1 0);; {1}
(bigger five five 1 0);; {0}
(lesser four five 1 0);; {1}
(lesser five five 1 0);; {0}
(to_int (div2 ten two));; {5}
(to_int (div2 ten three));; {3}
